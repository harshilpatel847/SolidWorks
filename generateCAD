' ******************************************************************************
' Create CAD model for specialty shape foam product from DXF/DWG
' Harshil Patel
' July 7, 2025
' Scrub Daddy, Inc.
'
' This macro generates a CAD model of the selected foam product from the provided
' artwork (DXF or DWG file)
'   a) Scrub Daddy (39.70mm thick Flextexture)
'   b) Scrub Mommy (19.85mm thick Flextexture, 19.85mm thick Resofoam)
'   c) TODO: Scrub Daddy Essentials (25.40mm thick Flextexture)
'   d) TODO: Scrub Mommy Essentials (12.70mm thick Flextexture, 12.70mm thick Resofoam)
'
' The generated model includes datums and features that will be used for
' dimensioning in the engineering drawing
'
' The model is saved based on a pre-defined naming scheme and data from the UserForm
'
' ******************************************************************************

Option Explicit

Dim swApp As SldWorks.SldWorks
Dim swModel As SldWorks.ModelDoc2
Dim swModelDocExt As SldWorks.ModelDocExtension
Dim fileName As String
Dim importData As SldWorks.ImportDxfDwgData
Dim subCategory As String
Dim subCategoryFormatted As String
Dim retVal As Boolean
Dim boolStatus As Boolean
Dim flextextureExtrude As Object
Dim resofoamExtrude As Object
Dim swRefPlane As SldWorks.RefPlane
Dim swFeatMgr As SldWorks.FeatureManager
Dim swBoundingBoxFeat As SldWorks.Feature
Dim swBoundingBoxFeatDef As SldWorks.BoundingBoxFeatureData
Dim selectedPath As String
Dim newProductName As String
Dim longStatus As Long
Dim longErrors As Long, longWarnings As Long

Sub main()

    ' User input box to get the path of the DXF file
    Set swApp = Application.SldWorks
    fileName = newProductSetup.artworkFilePath.Text
    Set importData = swApp.GetImportFileData(fileName)

    ' Identify the product sub-category (Scrub Daddy or Scrub Mommy, regular or Essentials)
    If newProductSetup.scrubDaddyOption.Value = True Then
        subCategory = "Daddy"
    ElseIf newProductSetup.scrubMommyOption.Value = True Then
        subCategory = "Mommy"
    ElseIf newProductSetup.scrubDaddyEssentialOption.Value = True Then
        subCategory = "Daddy Essential"
    ElseIf newProductSetup.scrubMommyEssentialOption.Value = True Then
        subCategory = "Mommy Essential"
    End If

    ' Import method
    importData.ImportMethod("") = swConst.swImportDxfDwg_ImportMethod_e.swImportDxfDwg_ImportToPartSketch

   ' Load the specified DXF/DWG file
    Set swModel = swApp.LoadFile4(fileName, "", importData, longErrors)
    importData.AddSketchConstraints("") = True
    retVal = importData.SetMergePoints("", True, 0.000002)
    importData.ImportDimensions("") = True
    importData.ImportHatch("") = False

    ' Create boss extrude from the sketch
    Set swModel = swApp.ActiveDoc
    boolStatus = swModel.Extension.SelectByID2("Model", "SKETCH", 0, 0, 0, False, 0, Nothing, 0)
    If subCategory = "Daddy" Then
        ' Boss extrude to create the Flextexture product
        Set flextextureExtrude = swModel.FeatureManager.FeatureExtrusion3(True, False, False, 0, 0, 0.0397, 0, False, False, False, False, 0, 0, False, False, False, False, True, True, True, 0, 0, False)
    ElseIf subCategory = "Mommy" Then
        ' Boss extrude to create the Flextexture portion of the product
        Set flextextureExtrude = swModel.FeatureManager.FeatureExtrusion3(True, False, False, 0, 0, 0.01985, 0, False, False, False, False, 0, 0, False, False, False, False, False, True, True, 0, 0, False)
        boolStatus = swModel.Extension.SelectByID2("Model", "SKETCH", 0, 0, 0, False, 0, Nothing, 0)
        ' Boss extrude to create the Resofoam portion of the product
        Set resofoamExtrude = swModel.FeatureManager.FeatureExtrusion3(True, False, True, 0, 0, 0.01985, 0, False, False, False, False, 0, 0, False, False, False, False, False, True, True, 0, 0, False)
    ElseIf subCategory = "Daddy Essential" Then
        ' Boss extrude to create the Flextexture product
        Set flextextureExtrude = swModel.FeatureManager.FeatureExtrusion3(True, False, False, 0, 0, 0.0254, 0, False, False, False, False, 0, 0, False, False, False, False, True, True, True, 0, 0, False)
    ElseIf subCategory = "Mommy Essential" Then
        ' Boss extrude to create the Flextexture product
        Set flextextureExtrude = swModel.FeatureManager.FeatureExtrusion3(True, False, False, 0, 0, 0.0127, 0, False, False, False, False, 0, 0, False, False, False, False, True, True, True, 0, 0, False)
        boolStatus = swModel.Extension.SelectByID2("Model", "SKETCH", 0, 0, 0, False, 0, Nothing, 0)
        ' Boss extrude to create the Resofoam product
        Set resofoamExtrude = swModel.FeatureManager.FeatureExtrusion3(True, False, True, 0, 0, 0.0127, 0, False, False, False, False, 0, 0, False, False, False, False, False, True, True, 0, 0, False)
    End If
    swModel.ClearSelection2 True
        
    ' Create bounding box to be used in drawing to establish overall height, width
    Set swFeatMgr = swModel.FeatureManager
    Set swBoundingBoxFeatDef = swFeatMgr.CreateDefinition(swConst.swFmBoundingBox)
    boolStatus = swModel.Extension.SelectByID2("Top Plane", "PLANE", 0, 0, 0, True, 0, Nothing, 0)
    swBoundingBoxFeatDef.ReferenceFaceOrPlane = swConst.swGlobalBoundingBoxFitOptions_e.swBoundingBoxType_CustomPlane
    Set swBoundingBoxFeat = swFeatMgr.CreateFeature(swBoundingBoxFeatDef)
    swModel.ClearSelection2 True
    
    ' Save the part using a name provided by the user.
    ' The new filename will take the form of scrubDaddy_XXXXX, scrubDaddyEssentials_XXXXX, etc. where XXXXX is the name provided
    selectedPath = newProductSetup.saveDirectory.Text
    newProductName = newProductSetup.productName.Text
    subCategoryFormatted = Replace(subCategory, " ", "")
    longStatus = swModel.Extension.SaveAs(selectedPath & "\scrub" & subCategoryFormatted & "_" & newProductName & ".SLDPRT", swSaveAsCurrentVersion, swSaveAsOptions_Silent, Nothing, longErrors, longWarnings)
    
    ' Change dimension properties depending on product sub-category
    Set swModelDocExt = swModel.Extension
    If subCategory = "Daddy" Or subCategory = "Daddy Essential" Then
        boolStatus = swModel.Extension.SelectByID2("D1@Boss-Extrude1@scrub" & subCategoryFormatted & "_" & newProductName & ".SLDPRT", "DIMENSION", 0, 0, 0, False, 0, Nothing, 0)
        boolStatus = swModel.EditDimensionProperties2(0, 0, 0, "", "", False, 1, 2, True, 12, 12, "", "", True, "", "", False)
    ElseIf subCategory = "Mommy" Or subCategory = "Mommy Essential" Then
        boolStatus = swModel.Extension.SelectByID2("D1@Boss-Extrude1@scrub" & subCategoryFormatted & "_" & newProductName & ".SLDPRT", "DIMENSION", 0, 0, 0, False, 0, Nothing, 0)
        boolStatus = swModel.EditDimensionProperties2(swTolSYMMETRIC, 0.0005, 0.0005, "", "", False, 2, 2, True, 12, 12, "", "", True, "", "", False)
        boolStatus = swModel.Extension.SelectByID2("D1@Boss-Extrude2@scrub" & subCategoryFormatted & "_" & newProductName & ".SLDPRT", "DIMENSION", 0, 0, 0, False, 0, Nothing, 0)
        boolStatus = swModel.EditDimensionProperties2(swTolSYMMETRIC, 0.0005, 0.0005, "", "", False, 2, 2, True, 12, 12, "", "", True, "", "", False)
    End If
    
End Sub
